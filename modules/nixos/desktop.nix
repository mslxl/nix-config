{
  username,
  lib,
  config,
  pkgs,
  ...
}: let
  cfgWayland = config.modules.desktop.wayland;
in {
  imports = [
    ./base
    ../base

    ./desktop
  ];

  options.modules.desktop = {
    wayland = {
      enable = lib.mkEnableOption "Wayland Display Server";
    };
  };

  config = lib.mkMerge [
    (lib.mkIf cfgWayland.enable {
      ####################################################################
      #  NixOS's Configuration for Wayland based Window Manager
      ####################################################################
      services = {
        xserver.enable = false; # disable xorg server
        # https://wiki.archlinux.org/title/Greetd
        greetd = {
          enable = true;
          settings = {
            default_session = {
              user = username;
              # .wayland-session is a script generated by home-manager, which links to the current wayland compositor(sway/hyprland or others).
              # with such a vendor-no-locking script, we can switch to another wayland compositor without modifying greetd's config here.
              # command = "${pkgs.tuigreet}/bin/tuigreet --time --cmd Hyprland"; # start Hyprland
              # command = "$HOME/.wayland-session"; # start a wayland session directly without a login manager
              command = "${pkgs.tuigreet}/bin/tuigreet --time --cmd $HOME/.wayland-session"; # start wayland session with a TUI login manager
            };
          };
        };
      };

      # fix https://github.com/ryan4yin/nix-config/issues/10
      security.pam.services.swaylock = {};
    })
  ];
}
